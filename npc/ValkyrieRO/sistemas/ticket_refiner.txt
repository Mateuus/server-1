//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,193,171,3	script	Refine Master	813,{
	disable_items;
	if (countitem(6238) || countitem(6228) || countitem(6229) || countitem(6230) || countitem(6231) || countitem(6456))
		set .@bWeaponUp,1;
	if (countitem(6239) || countitem(6232) || countitem(6233) || countitem(6234) || countitem(6235) || countitem(6457))
		set .@bArmorUp,1;
	if (!.@bWeaponUp && !.@bArmorUp) {
		mes "[Refine Master]";
		mes "Hola!";
		mes "¿Que pasa?";
		mes "Soy especialista";
		mes "para refinado de los item,";
		mes "pero yo no trabajo más.";
		next;
		switch(select("Voy a seguir mi camino.:Hmm... esto me hace curioso.")) {
		case 1:
			mes "[Refine Master]";
			mes "Tenga cuidado, aventurero.";
			close;
		case 2:
			mes "[Refine Master]";
			mes "En realidad, a veces proporciono servicios refinar para aventureros con un ^006400Refine Ticket^000000...";
			mes "Adiós bye~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Refine Master]";
	mes "¡Saludos!";
	mes "Puedo refinar un item hasta el ^006400mismo nivel que su ticket^000000.";
	mes "Usted no tiene que preocuparse! No hay ninguna posibilidad de romper su item.";
	next;
	if(select("Volveré más tarde.:Refinar item con ticket.") == 1) {
		mes "[Refine Master]";
		mes "Okay.";
		mes "Puede venir de nuevo más tarde.";
		close;
	}
	mes "[Refine Master]";
	mes "Qué equipo le gustaría refinar?";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head middle","Head lower";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@i))?getequipname(.@i):.@position$[.@i]+"- [Empty]")+":";
	set .@part, select(.@menu$);
	if (!getequipisequiped(.@part)) {
		mes "[Refine Master]";
		mes "sted tiene que equipar el objeto que desea refinar.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Refine Master]";
		mes "Oh lo siento.";
		mes "Este item no es posible refinar.";
		close;
	}
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		setarray .@tickets[0],6457,6235,6234,6233,6232,6239;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Armor";
		set .@check,.@bArmorUp;
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		setarray .@tickets[0],6456,6231,6230,6229,6228,6238;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Weapon";
		set .@check,.@bWeaponUp;
		break;
	}
	if (!.@check) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "Si desea refinar este ^006400"+.@type$+"^000000, por favor venga junto con ^006400"+.@type$+" Refine Ticket^000000.";
		mes "¡Nos vemos más tarde!";
		close;
	}
	mes "[Refine Master]";
	mes "Por favor, elija la que ^006400"+.@type$+" Refine Ticket^000000 que desea utilizar.";
	next;
	set .@menu$,"";
	for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1)
		set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
	set .@select, select(.@menu$)-1;
	set .@ticket_lv, .@levels[.@select];
	set .@ticket_id, .@tickets[.@select];
	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Refine Master]";
		mes getitemname(.@ticket_id)+" no está en su inventario. ¿Ha puesto en su almacenamiento?";
		mes "Por favor revise de nuevo.";
		mes "¡Nos vemos más tarde!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Refine Master]";
		mes "^8B4513This item is already refined as much as your deed.^000000";
		mes "Por favor, venir junto con un item refinado menos de ticket.";
		close;
	}
	mes "[Refine Master]";
	mes "Voy a refinar ^006400"+getequipname(.@part)+"^8B4513 hasta el +"+.@ticket_lv+" nivel^000000 con ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "¿Puedo continuar?";
	next;
	if(select("No.:Si.") == 1) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "Oh, ha cambiado de opinión.";
		mes "Ok.";
		mes "Puede volver más tarde.";
		close;
	}
	mes "[Refine Master]";
	mes "Great.";
	mes "¡Como desées!";
	mes "Tengo mi propia manera especial para refinar...";
	mes ".......ka boom!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id))
		delitem .@ticket_id,1;
	else {
		next;
		mes "Error!";
		mes "Por favor, informe de esto.";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Refine Master]";
	mes "Muy bien, aquí está~";
	mes "Bien, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "Felicitaciones por su brillante "+.@type$+".";
	mes "¡Te ves genial!";
	mes "Adiós~!";
	close;

OnTouch:
	specialeffect EF_REPAIRWEAPON;
	end;

}
