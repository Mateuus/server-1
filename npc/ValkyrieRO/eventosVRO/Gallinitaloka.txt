//===== Athena Script =====================================
//= Cluck! Cluck! Boom!
//===== By Keale of VoidRO ================================
//= http://voidro.com
//===== Description =======================================
//= Click the chicken and try retrieve the item at a low
//= chance. If you fail he will nuke, freeze, stone,
//= stun, or make you fall asleep.
//= The prize is configurable and triggered by the NPC.
//===== Credits ===========================================
//= LuTze for his 'Chicken of Punishment' script.
//= BrianL for suggesting the 'switch' command.
//===== Version ===========================================
//= v1.1
//=========================================================
//= v1.0 - First release.
//= v1.1 - Using 'switch rand' instead.
//=========================================================

prontera,155,220,4	script	Cluckers	800,{

if ($@startcluck == 1) goto L_playcluck;

cluckcluck:
	if (getgmlevel() >= 99) goto cluckadmin;
		mes "[Cluckers]";
		mes "Cluck cluck! Cluuuuuck?";
		mes "Cluck....";
	close;

cluckadmin:
		mes "[Cluckers]";
		mes "Cluck cluck! Cluuuuuck? ^FF0000~Hola GM " + strcharinfo(0) + ", Quieres jugar hoy?~^000000";
		mes "Cluck cluck... CLUCK! ^FF0000~solo dime que hacer!~^000000";
		switch(select("Iniciar Evento:Verificar Premio:Definir Premio:Hoy no Cluckers")) {
case 1:
		next;
		mes "[Cluckers]";
		mes "CLUCK! ^FF0000~Seguro!~^000000";
		emotion 33;
		close2;
		goto L_cluckannounce;
case 2:
		next;
		mes "[Cluckers]";
		mes "Cluck, cluck cluck... Cluck! ^FF0000~El Premio actualmente es^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +".^000000";
		next;
		goto cluckadmin;
case 3:
		next;
		mes "[Cluckers]";
		mes "Cluck cluck? Cluck??? ^FF0000~Cual sera el premio para el ganador? Por favor, ingresa el ID.~^000000";
		input $cluck_item_id;
		next;
		mes "[Cluckers]";
		mes "Cluck? cluuuck? ^FF0000~Cuantos de estos items deberia dar?~^000000";
		input $cluck_item_amount;
		next;
		mes "[Cluckers]";
		mes "Cluck cluck..? Cluck. ^FF0000~Asi que, el premio es^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +"^000000? ^FF0000Genial.~^000000";
		emotion 33;
		next;
		goto cluckadmin;
case 4:
		next;
		mes "[Cluckers]";
		mes "Cluck cluck cluck...";
		close;
}

L_cluckannounce:
		announce "[Cluck! Cluck! Boom!] esta a punto de comenzar en Prontera!",bc_blue;
		initnpctimer;
		end;
OnTimer10000:
		announce "Apresurense a llegar a atras de la estatua si desean jugar con el pollo loco!",bc_blue;
		end;
OnTimer20000:
		announce "Cluckers se ha comido uno de mis items! Me da miedo recuperarlo!",bc_blue;
		end;
OnTimer30000:
		announce "Da Click al pollo desquiciado y trata de sacarle el item, con algo de suerte ganaras! Estas listo?",bc_blue;
		end;
OnTimer40000:
		announce "Vamos! Den Click al pollo para ganar el premio!",bc_blue;
		set $@startcluck,1;
		end;

L_playcluck:
		specialeffect2 2;
		switch( rand(15) ) {
case 0:
		npctalk "CLUUUUUUCK!!!";
		emotion 23;
		atcommand "@nuke "+strcharinfo(0);
		break;
case 1:
		npctalk "Cluuuuuck!~";
		break;
case 2:
		atcommand "@nuke "+strcharinfo(0);
		break;
case 3:
		sc_start SC_Freeze,10000,0;
		break;
case 4:
		npctalk "CLUUUUUUUUUCK!!!";
		emotion 23;
		atcommand "@nuke "+strcharinfo(0);
		break;
case 5:
		sc_start SC_Sleep,10000,0;
		break;
case 6:
		emotion 29;
		sc_start SC_Stone,10000,0;
		break;
case 7:
		npctalk "CLUUUUUUCK!!!";
		emotion 23;
		atcommand "@nuke "+strcharinfo(0);
		break;
case 8:
		npctalk "Cluck! CLUUUCK!!";
		emotion 23;
		atcommand "@nuke "+strcharinfo(0);
		break;
case 9:
		sc_start SC_Stun,10000,0;
		break;
case 10:
		emotion 29;
		sc_start SC_Sleep,10000,0;
		break;
case 11:
		npctalk "Cluck! Cluck!";
		break;
case 12:
		sc_start SC_Stun,10000,0;
		break;
case 13:
		atcommand "@nuke "+strcharinfo(0);
		break;
default:
		if( rand(50) < 3 ) {
			npctalk "WOOF!...........";
			specialeffect2 72;
			announce "[Cluck! Cluck! Boom!] " + strcharinfo(0) + " logro sacar el item del pollo! Bien hecho!",0;
			getitem $cluck_item_id,$cluck_item_amount;
			set $@startcluck,0;
		} else {
			npctalk "Cluck! CLUUUCK!!";
			atcommand "@nuke "+strcharinfo(0);
			}
		break;
	}
}