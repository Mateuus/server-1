/*====================================================================================*/
/*==============================Monster Invasion Event================================*/
/*====================================================================================*/
/*==============================By:Night Terror=======================================*/
/*====================================================================================*/
/*=================Translated into Spanish by: BlackScythe (InFiNiTe)=================*/
/*===Inspired by: Automated MVP Event by xMachina / Acetito and Emistry===============*/
/*=== v0.9 ================================================Spanish Version============*/


function	script	F_IsMapValid	{ 				//Revisa si el mapa esta seteado
	if(getarg(0)!="")
		return "["+getarg(0)+"]";
	else
		return "";
}

function	script	F_IsItemValid	{				//Revisa si el objeto fue seteado
	if(getarg(0)!=0)
		return "["+getitemname(getarg(0))+" x "+$@Amount+"]";
	else
		return "";
}

function	script	F_IsMobValid	{				//Revisa si el monstruo es seteado
	if(getarg(0)!=0)
		return "["+getmonsterinfo(getarg(0),0) + " x " +getarg(1)+"]";
	else
		return "";
}

function	script	F_ChooseMob	{					//Monstruos & Lista de monstruos 

	
	switch(select("Monstruos Debiles:Monstruos Fuertes")){
	
		Case 1:
		
		set @Monsters[getarg(0)],$@Monsteridstrong[select("Arclouse:Baba Yaga:Baby Leopard:Baphomet Jr.:Bloody Butterfly:Dark Frame:Dragon Tail:Evil Cloud Hermit:Giant Whisper:Grand Peco:Greatest General:High Orc:Hill Wind:Hyegun:Hylozoist:Li Me Mang Ryang / Jing Guai:Live Peach Tree / Enchanted Peach Tree:Marine Sphere:Miyabi Ningyo / Miyabi Doll:Myst Case:Nereid:Orc Archer:Orc Baby:Raydric Archer:Remover:Sea Otter:Side Winder:Stone Shooter:Wanderer:Wind Ghost:Wooden Golem")]; 

		break;
			
		
		Case 2:	
		
		set @Monsters[getarg(0)],$@Monsteridweak[select("Am Mut:Ancient Mummy:Firelock Soldier:Armeyer Dinze:Bloody Knight:Bloody Murderer:Bow Guardian:Cat o' Nine Tails:Dark Illusion:Eremes Guile:Errende Ebecee:Howard Alt-Eisen:Ice Titan:Imp:Kavach Icarus:Knight of Abyss:Loli Ruri:Margaretha Sorin:Mutant Dragonoid:Observation:Owl Duke:Ragged Zombie:Laurell Weinder:Retribution:Sword Guardian:The Paper:Tirfing/Ogretooth:Wickebine Tres:Wicked Nymph:Zealotus:Zombie Prisoner")]; 

		break;
	}	
	for (set @i,0; @i < 7; set @i,@i+1)	{			//Revisa si los monstruos ya fueron elegidos
		if (@Monsters[getarg(0)]==@Monsters[@i] && @i!=getarg(0)){ 			//Limpia la lista de monstruos si el monstruo ya fue elegido
			set @Monsters[getarg(0)],0;
		}
	}
	
	input @MobAmount[getarg(0)],20,300;

	return 0;


}

function	script	F_ClearGarbagez	{				//Limpia los cuadros anteriores

	cleararray @Monsters,0,7;
	cleararray @MobAmount,0,7;
	set $@ItemID,0;
	set $@Map$,"prontera";
	return 0;
}


prontera,147,158,5	script	Monster Invasion Helper::MonsterInvasion	402,{ 


	//define monsterlist and itemlist
	setarray $@Monsteridweak[1],1301,1297,1403,1654,1268,1507,1830,1307,1302,1635,1655,1636,1777,1837,1656,1219,1502,1637,1262,1700,1320,1865,1657,1702,1829,1375,1204,1653,1416,1200,1197;
	setarray $@Monsteridstrong[1],1194,1882,1415,1101,1408,1260,1321,1412,1186,1369,1277,1213,1680,1512,1510,1517,1410,1142,1404,1249,1255,1189,1686,1276,1682,1323,1037,1495,1208,1236,1497;
	setarray $@Healitem[1],521,12321,505,548,547,12123,12299,12320,12016,607;
	setarray $@Useableitem[1],662,12354,12257,12704,14598,12212,12135,14607,12268,12128,12266,12310,14601,12019;
	
	if(getgmlevel() < 60) 				//Revisa si el nivel de GM es lo suficientemente alto
		warp "prontera",156,184;
		
	if($@Event!=0){									//Revisa si el evento esta corriendo actualmente
		mes "[ ^0C66C0Event Helper^000000 ]";
		mes "¡El evento ya ha iniciado y esta corriendo actualmente!";
		close;
	}
	set $@Map$,"prontera";				//Mapa por defecto: Prontera
	while(1)
	{
		mes "[ ^0C66C0Event Helper^000000 ]";
		mes "Por favor, selecciona los parametros.";
		next;
		switch(select("Objeto "+callfunc("F_IsItemValid",$@ItemID)+":Mapa " +callfunc("F_IsMapValid",$@Map$)+":Monstruo:Borrar los valores actuales:¡Iniciar evento!")){
			Case 1:   
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "¡La recompenza sera para el top 10 de mata monstruos!.";
					mes "El personaje que mas monstruos mate sera el que elija la cantidad de items que desea recibir, Los que queden mas abajo en el rank  Recibiran 10% menos por rank.";
					next;
					message strcharinfo(0),"¡Por favor, selecciona el objeto!";
					switch(select("Objetos curativos:Objetos usables")){			//Objetos y lista de cantidad de objetos
						Case 1:
						
							set $@ItemID,$@Healitem[select("Aloe Leaf:Arunafeltz Desert Sandwich:Blue Potion:Cheese:White Slim Potion:Honey Pastry:Status Resist Potion:Poison Bottle:Schwartzwald Pine Jubilee:Speed Potion:Yggdrasil Berry")]; 	
		
							break;
						Case 2:
											
							set $@ItemID,$@Useableitem[select("Authoritative Badge:Buche De Noel:Cold Medicine:Elixir of Life:Ghostring Scroll:Giant Fly Wing:Green Ale:Luxurious Western Food:Rainbow Cake:Royal Cooking Kit:Sesame Pastry:Spray Of Flowers:Tyr's Blessing:Holy Egg")];
						
							break;
							
					}
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "Selecciona la cantidad de objetos deseada, 1 es el minimo, 100 es el maximo.";
					next;
					input $@Amount,1,100;								
					break;						
			Case 2:
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "¡Ahora escoje el mapa!";
					mes "Obviamente, el mapa tiene que ser valido.";    
					next;
					do{								//Regresa un mapa valido
					input $@Map$;}while(getmapmobs($@Map$) == -1 );
					break;
			Case 3:
			
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "¿Que monstruos deseas invocar?";
					mes "Puedes invocar 7 monstruos diferentes.";
					mes "La cantidad minima de monstruos es 20, la maxima es 300";
					next;
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "Por favor elige los monstruos y la cantidad.";
					next;
					//Stay in menu until player has chosen all monsters he wants
					while(1){
						set @ChosenPlace,select("Mob 1 - "+callfunc("F_IsMobValid",@Monsters[0],@MobAmount[0])+":Mob 2 - "+callfunc("F_IsMobValid",@Monsters[1],@MobAmount[1])+":Mob 3 - "+callfunc("F_IsMobValid",@Monsters[2],@MobAmount[2])+":Mob 4 - "+callfunc("F_IsMobValid",@Monsters[3],@MobAmount[3])+":Mob 5 - "+callfunc("F_IsMobValid",@Monsters[4],@MobAmount[4])+":Mob 6 - "+callfunc("F_IsMobValid",@Monsters[5],@MobAmount[5])+":Mob 7 - "+callfunc("F_IsMobValid",@Monsters[6],@MobAmount[6])+":No mas monstruos.")-1;
						
						if(@ChosenPlace>6) break;
							callfunc("F_ChooseMob",@ChosenPlace);
					}
					break;	
						
				
			Case 4:	
					callfunc("F_ClearGarbagez");
					break;
			Case 5:    

					if( $@Map$ == "" || @Monsters[0] == 0){				//Revisa si los monstruos y los mapas fueron seteados
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "Por fvor, revisa que todo este en orden.";
					next;
					break;
					}

					setarray $@randnum[0],rand(30,60),rand(200,300),rand(450,500),rand(900,1000);
					mes "[ ^0C66C0Event Helper^000000 ]";
					mes "¡El evento ha iniciado!";
					set $@Event,1;
					

					close2;
					announce "Los monstruos han sido avistados en "+$@Map$+"! ¡Retirenlos de ahi!",bc_all,0xFFFF86;
					if ($@ItemID != 0)
					announce "La recompenza para el mejor asesino de mobs sera "+$@Amount+" x "+getitemname($@ItemID)+". Subsequentemente, los demas recibiran 10% menos hasta el top 10, es decir entre menos mobs mates menos recibes..." ,bc_all,0xFFFF86;
					sleep2 2500;
					announce "Los siguientes monstruos fueron avistados:",bc_all,0xB7D2E9;
					sleep2 1000;
					for (set @i,0; @i < 7; set @i,@i+1){				//Aparicien de monstruos y sus anuncios
						if(@Monsters[@i]!=0){
						announce getmonsterinfo(@Monsters[@i],0)+" x "+@MobAmount[@i],bc_all,0xB7D2E9;
						monster $@Map$,0,0,"--ja--",@Monsters[@i],@MobAmount[@i],strnpcinfo(3)+"::OnMobKill";
						}
					}

					end;
				
				
		}
		
		
	}
			
	    
	OnMobKill:	
		if (mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") <= $@randnum[0] && mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") > 5 || mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") == $@randnum[1] || mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") == $@randnum[2] || mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") == $@randnum[3]) 
		announce "Solo faltan "+mobcount($@Map$,strnpcinfo(3)+"::OnMobKill")+" Monstruos por matar.",bc_all,0xFFFF46;		//Anuncia los monstruos faltantes
		
		for(set @i,0; @i < 127; set @i,@i+1){							//Registra a quien mata y los puntos
			if($@killer$[@i] == ""){
					
				for (set @j,0; @j < 127; set @j,@j+1){
					if($@killer$[@i]==$@killer$[@j] && $@killer$[@i]!= "")
						goto Nameexists;			
				}

				set $@killer$[@i],strcharinfo(0);
				set $@kills[@i],$@kills[@i]+1;	
				
				break;
			
			}
			else if($@killer$[@i] == strcharinfo(0)){
				Nameexists:
				set $@kills[@i],$@kills[@i]+1;
				break;
			}

		}
		
		
		if(mobcount($@Map$,strnpcinfo(3)+"::OnMobKill") <=5){			//Si 5 o menos mobs permanecen vivos: Los mata a todos, Anuncia el top 15 de personas quienes mataron mas mobs, Anuncia el top 15 y entrega los premios (Si fueron seteados o establecidos antes) al top 10.
			killmonster $@Map$,"All";
			announce "¡La mayoria de los monstruos fueron derrotados!",bc_all,0xFFFF46;
			sleep2 2000;
			announce "¡Nuestra guardia local se encargara de eliminar a los monstruos restantes!",bc_all,0xFFFF46;
			sleep2 2000;
			announce "Hechemos un vistazo a los resultados:",bc_all,0xFFFF46;
			sleep2 1000;
			
			for (set @i,0; @i < 15; set @i,@i+1){						//Hace el anuncio del top
				for (set @j,0; @j < 15; set @j,@j+1){					
					if ($@kills[@j] < $@kills[@j+1]){					
						set @tmpkills, $@kills[@j+1];
						set @tmpkillers$, $@killer$[@j+1];
						set $@kills[@j+1], $@kills[@j];
						set $@killer$[@j+1],$@killer$[@j];
						set $@kills[@j], @tmpkills;
						set $@killer$[@j], @tmpkillers$;
					}
				}
			 }
			 
			for(set @i,0; @i < 15; set @i,@i+1){						//Y anuncia al top 15
				if($@killer$[@i] != "")
				announce @i+1+". Lugar: "+$@killer$[@i]+" con "+$@kills[@i]+" Monstuos matados.",bc_all,0xB7D2E9;
			}	
			
			
			if ($@ItemID != 0){											//Da el item si fue seteado, 10% menos por rank
				for(set @i,0; @i < 10; set @i,@i+1){
					if ($@killer$[@i]!=""){	
						getitem $@ItemID,$@Amount-$@Amount*@i/10,getcharid(3,$@killer$[@i]);	
						
					}
				}
			}
			
			
			logmes "Invasion de monstruos completada, El primer lugar del top es: " + $@killer$[0] + " Con "+$@kills[0]+ " muertes. La recompensa fue " +getitemname($@ItemID)+ " x "+$@Amount+". Esta fue la "+$Invasioncount+". invasion de monstruos.";
			set $Invasioncount,$Invasioncount+1;
			
			//Clear variables
			cleararray $@kills[0],0,200;
			cleararray $@killer$[0],"",200;
			cleararray $@flag[0],0,10;
			set $@Event,0; 

		}		
	
}

 








