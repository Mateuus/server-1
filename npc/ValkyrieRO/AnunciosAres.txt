//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~\\
//            Servicio de Anuncios 1.8           \\
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~\\
//                    Starky                     \\
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~\\
//    Desarrollado en eAthena-SQL-14284[Trunk]   \\
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~\\
// 1.0 - Primera version.                        \\
// 1.1 - Agregado menu colores.                  \\
// 1.2 - Agregada opcion Atras y Prev. mensaje.  \\
// 1.3 - Agregada opcion BroadCast Flash.        \\
// 1.4 - Agregado waitingroom "BroadCaster".     \\
// 1.5 - Agregado timer delay por turnos.        \\
// [ Check ]                                     \\
// 1.6 - Agregado mensaje [user] en los mensajes \\
//       enviados por los players.               \\
// 1.7 - Agregado sistema log.                   \\
// 1.8 - Agregada Var. rápida para cambiar color.\\
// 1.8a - Arreglado pequeño bug en el menú.      \\
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~\\

prontera,147,175,5	script	Servicio De Anuncios#01	725,{

	setarray .@c$[0],"FFFF00","FF0000","FF8000","00FF00","00FFFF","0000FF","FE2EF7"; //Colores seleccionables.

	set .@Log,0;       //Para que el broadcast enviado se guarde en el registro log de npc, [1 activado] [0 desactivado].
	set .TDelayer,10;  //Segundos de espera antes de poder hacer otro BroadCast, [0 desactivado].
	set .@z,100000;    //Precio por cada BroadCast ejecutado.

	set .nn$,"[ ^848484Servicio de anuncios^000000 ]"; //Nombre del npc.

	if (getwaitingroomstate(4)) waitingroom " [ BroadCaster ]",0;
	mes .nn$;
	mes "^FF0000BroadCast^000000 es un mensaje textual";
	mes "que se envia a la ventana de todos";
	mes "los users online en el servidor.";
	mes "Un ^FF0000BroadCast^000000 cuesta ^08088A"+.@z+" Zeny^000000.";
	mes " ";
	if (Zeny < .@z) {
		mes "^FF0000NO TIENES SUFICIENTE ZENY.^000000";
		close;
	}
	if (.TDelay > gettimetick(2)) {
		mes "^FF0000MODO ESPERA ( "+(.TDelay - gettimetick(2))+" ) SEGUNDOS.^000000";
		close;
	}
	mes "Que deseas hacer?";
	next;
	switch(select("BroadCast Flash.:BroadCast.:^880000Salir^000000.")) {
	case 1:
		mes .nn$;
		mes "Escribe el mensaje.";
		mes " ";
		mes " ";
		mes " ";
		mes " ";
		mes "Ingresa el Nº ( ^08088A0^000000 ) para salir.";
		next;
		input .@a$;
		if (.@a$ == "0") {
			mes .nn$;
			mes "Hasta luego.";
			close;
		}
		if(.TDelay > gettimetick(2)) {
			mes .nn$;
			mes "Debes esperar ^FF0000"+(.TDelay - gettimetick(2))+"^000000 segundos para volver a mandar otro mensaje.";
			close;
		}
		set .TDelay, gettimetick(2) + .TDelayer;
		doevent strnpcinfo(0)+"::OnRoom";

		mes .nn$;
		mes "Mensaje enviado.";
		set zeny,zeny - .@z;
		if (!getgmlevel()) set .@b$,"[user] ";
		announce .@b$+strcharinfo(0)+": "+.@a$,0,"0x"+.@c$[0];
		if (.@Log) logmes "[ BroadCaster Log ] "+.@a$;
		close;

	case 2: break;
	case 3:
		mes .nn$;
		mes "Hasta luego.";
		close;
	}
	while (.@d != 1) {
		mes .nn$;
		mes "Escribe el mensaje.";
		next;
		input .@a$;
		mes " [ ^B40404Informacion del mensaje^000000 ]";
		mes .@a$;
		next;
		set .@d,select("Continuar.:Atras.");
	}
	while (.@f != 1) {
		mes .nn$;
		mes "Selecciona el color del mensaje.";
		next;
		for (set .@a,0; .@a < getarraysize(.@c$); set .@a,.@a + 1) {
			set .@s$,"^"+.@c$[.@a]+"BroadCast.^000000"+((.@a + 1) == getarraysize(.@c$) ? "":":");
			set .@menu$,.@menu$ + .@s$;
		}
		set .@a,select(.@menu$);
		set .@a,.@a - 1;
		set .@menu$,"";

		mes " [ ^B40404Informacion del mensaje^000000 ]";
		mes "^"+.@c$[.@a]+.@a$+"^000000";
		next;
		set .@f,0;
		set .@e$,"0x"+.@c$[.@a];
		while (.@f != 1 && .@f != 3) {
			set .@f,select ("^FF0000Anunciar^000000.:Previsualizar mensaje.:Atras.");
			if (.@f == 2) announce strcharinfo(0)+": "+.@a$,bc_self,.@e$;
		}
	}
	if(.TDelay > gettimetick(2)) {
		mes .nn$;
		mes "Debes esperar ^FF0000"+(.TDelay - gettimetick(2))+"^000000 segundos para volver a mandar otro mensaje.";
		close;
	}
	set .TDelay, gettimetick(2) + .TDelayer;
	doevent strnpcinfo(0)+"::OnRoom";
	
	mes .nn$;
	mes "Mensaje enviado.";
	set zeny,zeny - .@z;
	if (!getgmlevel()) set .@b$,"[user] ";
	announce .@b$+strcharinfo(0)+": "+.@a$,0,.@e$;
	if (.@Log) logmes "[ BroadCaster Log ] "+.@a$;
	close;

OnRoom:
	if (!.TDelayer) end;
	delwaitingroom;
	waitingroom " [ Modo Espera ]",0;
	sleep (.TDelayer * 1000);
	delwaitingroom;
	waitingroom " [ BroadCaster ]",0;
	end;
}
