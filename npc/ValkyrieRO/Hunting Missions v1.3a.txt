//===== rAthena Script =======================================
//= Hunting Missions
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: =========================================
//= Random hunting missions.
//= Rewards are based on quest difficulty.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Small improvements and fixes.
//= 1.2 Added party support and replaced blacklists with an
//=     SQL query, both thanks to AnnieRuru.
//= 1.3 Re-added a blacklist adapted for the SQL query.
//= 1.3a Added mission reset options.
//============================================================
prontera,171,171,3	script	Busca Empleo?::MissionSign	837,{
	mes "^0000FF* Busca empleo?";
	mes "* Tiene habilidades para cazar criaturas?";
	mes "* Quiere ganar items y cash?^000000";
	mes "...";
	mes "Busque el npc [Caza de Misiones] entra no pierda más tiempo!";
	close;
}

prt_in,136,31,3	script	[Misiones de Caza]	10183,{
function Chk; function Cm;
	mes "[Caza de Misiones]";
	mes "Hola, "+strcharinfo(0)+"!";
	if (!#Mission_Delay) {
		next;
		mes "[Caza de Misiones]";
		mes "No puedo encontrar ningún registro...";
		mes "¡Debes ser nuevo aquí!";
		emotion ET_HUK;
		next;
		callsub Mission_Info;
		emotion ET_GO;
		set #Mission_Delay,1;
		close;
	}
	mes rand(2)?"Trabajando duro, como siempre...":"No aflojar, espero...";
	mes "¿Hay algo que te pueda ayudar?";
	mes " ";
	mes "^777777~ Ha completado ^0055FF"+Mission_Total+"^777777 mission"+((Mission_Total == 1)?"":"s")+". ~^000000";
	next;
	switch(select(((!Mission0)?" ^ff0000~^000000 Nueva misión::":": ^ff0000~^000000 Estado de la misión: ^ff0000~^000000 Abandonar la misión")+": ^ff0000~^000000 Información: ^ff0000~^000000 Tienda de misiones: ^ff0000~^000000 Ver los mejores cazadores: ^ff0000~^000000 ^777777Cancel^000000")) {
	case 1:
		mes "[Hunting Missions]";
		if (#Mission_Count) {
			mes "Has iniciado una misión";
			mes "En otro personaje.";
			close;
		}
		if (#Mission_Delay > gettimetick(2) && .Delay) {
			set .@i, #Mission_Delay-gettimetick(2);
			if (.@i > 3600) set .@j$, (.@i/3600)+" hour"+(((.@i/3600) == 1)?"":"s");
			else if (.@i > 60) set .@j$, (.@i/60)+" minute"+(((.@i/60) == 1)?"":"s");
			else set .@j$, (.@i)+" second"+((.@i == 1)?"":"s");
			mes "Me temo que tendrás que esperar "+.@j$+" antes de tomar otra misión.";
			close;
		}
		mes "Debes cazar:";
		query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 0 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT "+.Quests, .@mob);
		for (set .@i,0; .@i<.Quests; set .@i,.@i+1) {
			setd "Mission"+.@i, .@mob[.@i];
			setd "Mission"+.@i +"_",0;
		}
		set #Mission_Count, rand(.Count[0],.Count[1]);
		callsub Mission_Status;
		next;
		mes "[Hunting Missions]";
		mes "Reporte cuando haya terminado.";
		mes "¡Buena suerte!";
		close;
	case 2:
		mes "[Hunting Missions]";
		mes "Mission status:";
		callsub Mission_Status;
		close;
	case 3:
		mes "[Hunting Missions]";
		mes "¿De verdad quieres abandonar tu misión?";
		if (.Reset < 0 && .Delay)
			mes "Su tiempo de retardo no se restablecerá.";
		else if (.Reset > 0)
			mes "Costara "+Cm(.Reset)+" Zeny.";
		next;
		switch(select(" ~ Abandonar...: ~ ^777777Cancelar^000000")) {
		case 1:
			if (.Reset > 0) {
				if (Zeny < .Reset) {
					mes "[Hunting Missions]";
					mes "No tienes suficiente zeny para abandonar esta misión.";
					emotion ET_SORRY;
					close;
				}
				set Zeny, Zeny-.Reset;
				emotion ET_MONEY;
			}
			mes "[Hunting Missions]";
			mes "De acuerdo, he abandonado tu misión actual.";
			specialeffect2 EF_STORMKICK4;
			for(set .@i,0; .@i<.Quests; set .@i,.@i+1) {
				setd "Mission"+.@i,0;
				setd "Mission"+.@i+"_",0;
			}
			set #Mission_Count,0;
			if (.Reset < 0 && .Delay) set #Mission_Delay, gettimetick(2)+(.Delay*3600);
			close;
		case 2:
			mes "[Hunting Missions]";
			mes "¡Sabía que estabas bromeando!";
			mes "Sigan con el buen trabajo.";
			emotion ET_SMILE;
			close;
		}
	case 4:
		callsub Mission_Info;
		close;
	case 5:
		mes "[Hunting Missions]";
		mes "Tienes ^0055FF"+#Mission_Points+"^000000 Mission Points.";
		mes "¡Utilícelos bien!";
		callshop "mission_shop",1;
		npcshopattach "mission_shop";
		end;
	case 6:
		mes "[Hunting Missions]";
		mes "Los mejores cazadores son:";
		query_sql("SELECT char_id AS id, (SELECT `name` FROM `char` WHERE char_id = id),`value` FROM `global_reg_value` WHERE str = 'Mission_Total' ORDER BY CAST(`value` AS SIGNED) DESC LIMIT 5",.@id,.@name$,.@val);
		for(set .@i,0; .@i<5; set .@i,.@i+1)
			mes "  [Rank "+(.@i+1)+"]  "+((.@name$[.@i] == "")?"^777777none":"^0055FF"+.@name$[.@i]+"^000000 : ^FF0000"+.@val[.@i]+" pt.")+"^000000";
		end;
	case 7:
		mes "[Hunting Missions]";
		mes "Nothing? Okay...";
		emotion ET_SCRATCH;
		close;
	}

Mission_Status:
	set @f,0;
	deletearray .@j[0], getarraysize(.@j);
	for(set .@i,0; .@i<.Quests; set .@i,.@i+1) {
		set .@j[.@i], getd("Mission"+.@i);
		set .@j[.Quests], .@j[.Quests]+strmobinfo(3,.@j[.@i]);
		set .@j[.Quests+1], .@j[.Quests+1]+(strmobinfo(6,.@j[.@i])/(getbattleflag("base_exp_rate")/100)*.Modifier[0]);
		set .@j[.Quests+2], .@j[.Quests+2]+(strmobinfo(7,.@j[.@i])/(getbattleflag("job_exp_rate")/100)*.Modifier[1]);
		mes " > "+Chk(getd("Mission"+.@i+"_"),#Mission_Count)+strmobinfo(1,.@j[.@i])+" ("+getd("Mission"+.@i+"_")+"/"+#Mission_Count+")^000000";
	}

	// Reward formulas:
	set .@Mission_Points, 3+(.@j[.Quests]/.Quests/6);
	set .@Base_Exp, #Mission_Count*.@j[.Quests+1]/5;
	set .@Job_Exp, #Mission_Count*.@j[.Quests+2]/5;
	set .@Zeny, #Mission_Count*.Quests*.@j[.@i]*.Modifier[2];

	next;
	mes "[Hunting Missions]";
	mes "Recompensas de la misión:";
	mes " > Mission Points: ^0055FF"+.@Mission_Points+"^000000";
	mes " > Base Experience: ^0055FF"+Cm(.@Base_Exp)+"^000000";
	mes " > Job Experience: ^0055FF"+Cm(.@Job_Exp)+"^000000";
	mes " > Zeny: ^0055FF"+Cm(.@Zeny)+"^000000";
	if (@f) { set @f,0; return; }
	next;
	mes "[Hunting Missions]";
	mes "Oh, ya terminaste!";
	mes "Buen trabajo.";
	mes "Aquí está tu recompensa.";
	emotion ET_BEST;
	specialeffect2 EF_ANGEL;
	specialeffect2 EF_TRUESIGHT;
	set #Mission_Points, #Mission_Points+.@Mission_Points;
	set BaseExp, BaseExp+.@Base_Exp;
	set JobExp, JobExp+.@Job_Exp;
	set Zeny, Zeny+.@Zeny;
	for(set .@i,0; .@i<.Quests; set .@i,.@i+1) {
		setd "Mission"+.@i,0;
		setd "Mission"+.@i+"_",0;
	}
	set #Mission_Count,0;
	if (.Delay) set #Mission_Delay, gettimetick(2)+(.Delay*3600);
	set Mission_Total, Mission_Total+1;
	if (Mission_Total == 1) query_sql("INSERT INTO `global_reg_value` (`char_id`,`str`,`value`,`type`,`account_id`) VALUES ("+getcharid(0)+",'Mission_Total','1',3,0)");
	else query_sql("UPDATE `global_reg_value` SET `value` = "+Mission_Total+" WHERE char_id = "+getcharid(0)+" AND `str` = 'Mission_Total'");
	close;

Mission_Info:
	mes "[Hunting Missions]";
	mes "Si así lo eliges, puedo asignar";
	mes "una búsqueda aleatoria de caza.";
	mes "Algunos son más fáciles que otras,";
	mes "pero las recompensas aumentan con dificultad.";
	next;
	mes "[Hunting Missions]";
	mes "Los puntos de Misiones son compartidos";
	mes "entre todos tus personajes.";
	if (.Delay) mes "El tiempo de demora es, también.";
	mes "No puedes tomar misiones en";
	mes "múltiples personajes a la vez.";
	next;
	mes "[Hunting Missions]";
	mes "Puedes comenzar una misión";
	if (.Delay) mes "cada "+((.Delay == 1)?"hora.":.Delay+" horas.");
	else mes "cuando quieras.";
	mes "Eso es todo~";
	return;

function Chk {
	if (getarg(0) < getarg(1)) { set @f,1; return "^FF0000"; }
	else return "^00FF00"; }

function Cm {
	set .@str$, getarg(0);
	for(set .@i,getstrlen(.@str$)-3; .@i>0; set .@i,.@i-3)
		set .@str$, insertchar(.@str$,",",.@i);
	return .@str$; }

OnBuyItem:
	set @cost,0;
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set @cost, @cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	mes "[Hunting Missions]";
	if (@cost > #Mission_Points) mes "You don't have enough Mission Points.";
	else {
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			dispbottom "Purchased "+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+".";
		}
		set #Mission_Points, #Mission_Points-@cost;
		mes "Deal completed.";
		emotion ET_MONEY;
	}
	set @cost,0;
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnNPCKillEvent:
	if (!getcharid(1) || !.Party) {
		if (!#Mission_Count || !Mission0) end;
		for (set .@i, 0; .@i<.Quests; set .@i,.@i+1) {
			if (strmobinfo(1,killedrid) == strmobinfo(1,getd("Mission"+.@i))) {
				if (getd("Mission"+.@i+"_") < #Mission_Count) {
					dispbottom "[Hunting Mission] Killed "+(set(getd("Mission"+.@i+"_"),getd("Mission"+.@i+"_")+1))+" of "+#Mission_Count+" "+strmobinfo(1,killedrid)+".";
					end;
				}
			}
		}
	} else if (.Party) {
		set .@mob, killedrid;
		getmapxy(.@map1$,.@x1,.@y1,0);
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i])) {
				attachrid $@partymemberaid[.@i];
				if (#Mission_Count && Mission0 && HP > 0) {
					getmapxy(.@map2$,.@x2,.@y2,0);
					if ((.@map1$ == .@map2$ || .Party == 1) && (distance(.@x1,.@y1,.@x2,.@y2) <= 30 || .Party < 3)) {
						for(set .@j,0; .@j<.Quests; set .@j,.@j+1) {
							if (strmobinfo(1,.@mob) == strmobinfo(1,getd("Mission"+.@j))) {
								if (getd("Mission"+.@j+"_") < #Mission_Count) {
									dispbottom "[Hunting Mission] Killed "+(set(getd("Mission"+.@j+"_"),getd("Mission"+.@j+"_")+1))+" of "+#Mission_Count+" "+strmobinfo(1,.@mob)+".";
									break;
								}
							}
						}
					}
				}
			}
		}
	}
	end;

OnInit:
	set .Delay,12;		// Quest delay, in hours (0 to disable).
	set .Quests,4;		// Number of subquests per mission (increases rewards).
	set .Party,3;		// Party options: 0 (exclude party kills), 1 (include party kills), 2 (same map only), 3 (screen area only)
	set .Reset,-1;		// Reset options: -1 (abandoning mission sets delay time), 0 (no delay time), [Zeny] (cost to abandon mission, no delay time)
	setarray .Count[0],	// Min and max monsters per subquest (increases rewards).
		40,70;
	setarray .Modifier[0],	// Multipliers for Base Exp, Job Exp, and Zeny rewards.
		getbattleflag("base_exp_rate")/100,getbattleflag("job_exp_rate")/100,60;
	setarray .Shop[0],	// Reward items: <ID>,<point cost> (about 10~20 points per hunt).
		5132,20,5137,30,5762,80,5780,30,18699,20,18740,10,5210,60;
	set .Blacklist$,	// Blacklisted mob IDs.
		"1062,1088,1183,1186,1200,1212,1220,1221,1234,1235,"+
		"1244,1245,1250,1268,1290,1293,1294,1296,1298,1299,"+
		"1300,1301,1303,1304,1305,1306,1308,1309,1311,1313,"+
		"1515,1588,1618,1676,1677,1678,1679,1796,1797,1974,"+
		"1975,1976,1977,1978,1979";

	npcshopdelitem "mission_shop",512;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "mission_shop", .Shop[.@i], .Shop[.@i+1];
	end;
}
-	shop	mission_shop	-1,512:-1
