//===== rAthena Script =======================================
//= Lord Kaho's Horns Quest
//===== By: ==================================================
//= ?
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Custom quest for Lord Kaho's Horns.
//===== Additional Comments: =================================
//= 1.1 Optimized, and fixed some requirements. [Euphy]
//============================================================

sky_quest,42,46,5	script	Lord Kaho's Servant	10187,{

	setarray .@Items[0],7005, 968, 923, 734, 724,2268,7036,7020,5053,7018,741,742,743,750,751,752,753,754;
	setarray .@Drops[0],1272,1087,1039,1260,1046,1115,1252,1147,1038,1059;
	set .@n$, "[Lord Kaho's Servant]";

	mes .@n$;
	mes "¡Trabajé hasta la muerte tratando de cumplir las expectativas ridículas de Lord Kaho para un headgear!";
	mes "Ahora que finalmente he encontrado la fórmula para el sombrero perfecto, estoy dispuesto a compartir mi tiempo y talentos.";
	next;
	mes .@n$;
	mes "Necesita lo siguiente para obtener el Kaho horns:";
	mes "3x ^0055FFEmperium^000000";
	next;
	mes .@n$;
	mes "Oh yeah...";
	mes "I also forgot to mention these...";
	next;
	mes .@n$;
	for(set .@i,0; .@i<10; set .@i,.@i+1)
		mes "1x ^0055FF"+getitemname(.@Items[.@i])+"^000000 (from "+strmobinfo(1,.@Drops[.@i])+")";
	next;
	mes .@n$;
	mes "We're not done yet, sweetie...";
	mes "I'm a big fan of dolls, so you need to bring me these cuties!";
	for(set .@i,10; .@i<18; set .@i,.@i+1)
		mes "1x ^0055FF"+getitemname(.@Items[.@i])+"^000000";
	next;
	mes .@n$;
	mes "Finally, I've worked hard to make these horns for my master with all the items mentioned above...";
	mes "Please include ^0055FF5 million Zeny^000000 for my efforts."; 
	next;
	mes .@n$;
	mes "Are you ready for me to make this special item?";
	next;
	if(select("Sure am!:These requirements are unfathomable!")==2) {
		mes .@n$;
		mes "What I had to go through was more unfathomable...";
		mes "If you succeed in getting these items, you will have incredible strength!";
		close; }
	mes .@n$;
	for(set .@i,0; .@i<18; set .@i,.@i+1)
		if (!countitem(.@Items[.@i])) { set .@nr,1; break; }
	if (.@nr || countitem(714) < 3 || Zeny < 5000000) {
		mes "I can't afford to make this if you don't bring all materials needed. Please understand this is to benefit heroes such as yourself!";
		close; }
	for(set .@i,0; .@i<18; set .@i,.@i+1)
		delitem .@Items[.@i],1;
	delitem 714,3;
	set Zeny, Zeny-5000000;
	mes "Wow! You are brave indeed!";
	getitem 5013,1;
	mes "Enjoy being God of Rune-Midgard!";
	close;

OnPCLoadMapEvent:
	showevent 1,0;
	end;
	}
