/*
CREATE TABLE IF NOT EXISTS `restock` (
  `charid` int(10) NOT NULL,
  `restockid` int(6) NOT NULL,
  `restkq` int(5) NOT NULL,
  `restkf` int(10) NOT NULL default '1'
) ENGINE=MyISAM DEFAULT CHARSET=latin1 ;

Script By Dastgir/Hercules
v1.0 - Intial Release
TO be used with Restock Plugin
*/

prontera,140,140,5	script	Restock	4_M_ALCHE_C,{
	end;

OnInit:
	query_sql "CREATE TABLE IF NOT EXISTS `restock` (`charid` int(10) NOT NULL,`restockid` int(6) NOT NULL,`restkq` int(5) NOT NULL,`restkf` int(10) NOT NULL default '1') ENGINE=MyISAM DEFAULT CHARSET=latin1 "; 
	bindatcmd("restock","Restock::OnAtcommandEnable",0,99);
	bindatcmd("restock2","Restock::OnAtcommand",0,99);
	end;

OnAtcommandEnable:
	if (restk == 1) {
		dispbottom("Restock System Disabled");
		restk = 0;
		end;
	}
	dispbottom("Restock System Enabled");
	restk = 1;
	end;

OnAtcommand:
	if (restk == 1) {
		dispbottom("Restock is Currently Enabled");
	}
	if (restk == 0) {
		dispbottom("Restock is Currently Disabled,use @restock to Enable");
		end;
	}
	.@quantity = atoi(.@atcmd_parameters$[1]);
	.@type = atoi(.@atcmd_parameters$[2]);
	.@type2 = atoi(.@atcmd_parameters$[3]);
	// itemid quantity type1 type2 isn't provided.
	if (.@atcmd_numparameters < 4) {
		dispbottom("Usage:");
		dispbottom("@restock2 <itemid> <quantity> <type1> <type2>");
		dispbottom("Type 1:");
		dispbottom("1 = Storage");
		dispbottom("2 = GStorage");
		dispbottom("Type 2:");
		dispbottom("1 = Add");
		dispbottom("2 = Remove");
		dispbottom("3 = Help");
		dispbottom("4 = List");
		dispbottom("5 = Empty The List");
		dispbottom("@restock2 x y z 3 For Help");
		dispbottom("@restock2 x y z 4 For Current Restocking List");
		dispbottom("@restock2 x y z 5 For Removing All Items From Restock");
		dispbottom("x,y,z Can be any number, e.g: @restock2 0 0 0 3 will give you help option,@restock2 x y z 5 will Empty your list");
		end;
	}
	// Type2 => Help
	if (.@type2 == 3) {
		dispbottom("Help:");
		dispbottom("If you want to restock 5 Orange Potion item from Storage, you can type");
		dispbottom("@restock2 502 5 1 1");
		dispbottom("If you want to restock the Orange Potion from GStorage,");
		dispbottom("@restock2 502 5 1 2");
		dispbottom("If you want to remove the restock of Orange Potion");
		dispbottom("@restock2 502 0 2 0");
		dispbottom("If You used restock from storage (@restock2 502 5 1 1) and then used same item from GStorage");
		dispbottom("then the value will be overwritten and the item will be restocked from GStorage.");
		end;
	}
	// Type2 => List Restock Item
	if (.@type2 == 4) {
		// Get List of Restock Items
		query_sql("SELECT `restockid`,`restkq`,`restkf` FROM `restock` WHERE `charid`="+ getcharid(0), .@id, .@qu, .@fr);
		if (.@id == 0) {
			dispbottom("No Item in Restock...");
			end;
		}
		.@size = getarraysize(.@id);
		dispbottom("------------------- List of Restock Items -------------------");
		for (.@i = 0; .@i < .@size; ++.@i) {
			// Show the List
			if (getitemname(.@id[@i]) !=  "null") {
				dispbottom("Restocking "+.@qu[.@i]+" "+getitemname(.@id[.@i])+" From "+(.@fr[.@i]==1?"Storage":"Guild Storage"));
			}
		}
		dispbottom("-------------------     End of List       --------------------");
		end;

	}
	// Type2 => Clear Restock Items
	else if (.@type2 == 5) {
		query_sql("DELETE FROM `restock` WHERE `charid`="+getcharid(0));
		dispbottom("All Restock Items Deleted.");
		end;
	}
	// Initial ItemID = 0
	.@item_id = 0;
	// Search by Item Name(if string given)
	.@item_search$ = .@atcmd_parameters$[0];
	if (atoi(.@item_search$) > 0) {
		.@item_id = atoi(.@item_search$);
	} else {
		.@found = searchitem(.@item_ids, .@item_search$);
		if (!.@found) {
			dispbottom("No Such Item Found");
			end;
		}
		dispbottom("Item Selected: "+ getitemname(.@item_ids[0]) +", Id: "+ .@item_ids[0]);
		.@item_id = .@item_ids[0];
	}
	// No Item ID Found
	if (!.@item_id) {
		dispbottom("No Item Selected.");
	} else if (getitemname(.@item_id) == "null") {
		dispbottom("Proper Item ID Please");
		end;
	}

	if (.@type <= 0 || .@type > 2) {
		dispbottom("Input Proper Type1 Please");
		end;
	}

	// Add Item to Restock List
	if (.@type2 == 1){
		// Invalid Quantity
		if (.@quantity < 1){
			dispbottom("Input Proper Quantity Please!");
			end;
		}
		// Check if existing entry exists
		query_sql("SELECT `restkf` FROM `restock` WHERE `charid`="+ getcharid(0) +" AND `restockid`="+ .@item_id, .@rf);
		// Update the table if existing entry exists
		if (.@rf) {
			query_sql("UPDATE `restock` SET `restkq`="+ .@quantity +", `restkf`="+ .@type +" WHERE `charid`="+ getcharid(0) +" AND `restockid`="+ .@item_id);
		} else {
			query_sql("INSERT INTO `restock` VALUES("+ getcharid(0) +","+ .@item_id +","+ .@quantity +","+ .@type +")");
		}
		// Check if Item Exists currently, if not then restock
		if (countitem(.@item_id) < .@quantity) {
			restkid = .@item_id;
			callsub OnRestock;
		}
		dispbottom("Restocking "+.@quantity+" "+getitemname(.@item_id)+" From "+((.@type==1)?"Storage":"Guild Storage")+"!");
		end;
	}
	// Remove Item from Restock List
	if (.@type2 == 2) {
		query_sql("DELETE FROM `restock` WHERE `charid`="+ getcharid(0) +" AND `restockid`="+ .@item_id);
		dispbottom("Deleted "+ .@item_id +" From Restocking!");
		end;
	}
	end;

OnRestock:
	// Get Restock Amount
	query_sql("SELECT `restkq`,`restkf` FROM `restock` WHERE `charid`="+ getcharid(0) +" AND `restockid`="+ restkid, .@qu, .@fr);
	if (.@qu) {
		// Restock Item
		.@ru = restkid;
		restock_item(.@ru, .@qu, .@fr);
	}
	restkid = 0;
	end;
}