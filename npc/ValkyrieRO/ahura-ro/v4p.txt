//====================================================================================
//Script Name: Vote For Points NPC Script for FluxCP
//SVN: Tested in rAthena r156513
//Developed By: JayPee Mateo
//Version: 1.0
//Requirement(s): FluxCP V4P Addon
//Description: This is a npc script for FluxCP Vote for points in order for the players
//to claim their vote points
//====================================================================================

npc_in,26,42,5	script	Sistema Votos	1_M_YOUNGKNIGHT,{

//Function Prototypes
function garbagecol;//Garbage collection for the Character variables
garbagecol();
function add_item; //Syntanx: add_item(ITEMID,QUANTITY,POINTS,CATEGORY);
function makeCategory;//This will return a list of the categories
function getItemsByCat;//This will return the list of items associated to the particular category
function getItemDetails;//This will return the details of the item
function getPoints;//This will return the points of the player stored in the database
function updatePoints;//This will updates the points of the player stored in the database


//NPC Name
set .npcname$,"[ Vote For Points ]";
	
//Initialization of the Rewards

add_item(12118,5,5,"[Fireproof Potion]");
add_item(12119,5,5,"[Coldproof Potion]");
add_item(12120,5,5,"[Earhproof Potion]");
add_item(14172,1,5,"[Assumptio 5 Scroll Box]");
add_item(14111,1,5,"[Abrasive 10 Box]");
add_item(12121,5,5,"[Thunderproof Potion]");
add_item(12114,3,6,"[Elemental Converter] Fire");
add_item(12115,3,6,"[Elemental Converter] Water");
add_item(12116,3,6,"[Elemental Converter] Earth");
add_item(12117,3,6,"[Elemental Converter] Wind");
add_item(12350,5,10,"[Angeling Potion]");
add_item(12299,3,10,"[Mega Resist Potion]");
add_item(12103,2,10,"[Bloody Branch]");
add_item(14091,1,12,"[Big Magic Defense Potion 10 Box]");
add_item(14085,1,12,"[Big Defense Potion 10 Box]");
add_item(12424,10,15,"[HP Increase Potion(Large)]");
add_item(7517,10,50,"[Gold Coin]");
add_item(7450,2,50,"[Skeletal Armor Piece]");
add_item(6231,1,70,"[Weapon Refine +6]");
add_item(7431,2,70,"[Three-Headed-Dragon's Head]");
add_item(14232,25,105,"[Yggdrasilberry10 Box]");
add_item(12028,250,105,"[Box of Thunder]");
add_item(12033,10,105,"[Box of Sunlight]");
add_item(12441,10,150,"[Old Bubble Gum]");
add_item(14545,3,150,"[Field Manual 300%]");

//Script Start	
			mes "[Asistente Votación]";
			mes "Hola, "+strcharinfo(PC_NAME)+".";
			mes "Cada vez que realizas una votación estarás obteniendo Puntos de Votación.";
			next;
			mes "[Asistente Votación]";
			mes "Los cuales puedes usar para intercambiar por items muy útiles.";
			set .@points,getPoints(getcharid(3));
			mes "Actualmente tienes "+.@points+" pt(s).";
			//garbagecol();
			next;
			mes "[Asistente Votación]";		
			mes "¿Qué deseas hacer?.";
	switch(select("Intercambiar Puntos:Nada"))
	{
		case 1:
		next;
			mes .npcname$;
			mes "Selecciona una categoria:";
			set .@selected,select(makeCategory())-1;
		next;
			mes .npcname$;
			mes "Selecciona el item que deseas:";
			set .@selected,select(getItemsByCat(@listCat$[.@selected]))-1;
			
		next;
			mes .npcname$;						
			set .@ritemid,getItemDetails(@itemKeys[.@selected],"itemid");
			set .@rquantity,getItemDetails(@itemKeys[.@selected],"quantity");
			set .@rpoints,getItemDetails(@itemKeys[.@selected],"points");
			mes "Item ID:"+.@ritemid;
			mes "Nombre Item: "+getitemname(.@ritemid);
			mes "Cantidad de Item: "+.@rquantity+" pc(s).";
			mes "Puntos Requeridos: "+.@rpoints+" pt(s).";
			mes "\n";
			mes "¿Quieres este item?";
			if(select("Si:No")==1)
			{
				set .@points,getPoints(getcharid(3));
				if(.@points>=.@rpoints)
				{
					next;
					mes .npcname$;			
					updatePoints(getcharid(3),.@rpoints);
					getitem .@ritemid,.@rquantity;
					mes "Trato Hecho!. Gracias por tu votacion. No olvides votar nuevamente :D";
				}
				else
					mes "Lo siento. No tienes los puntos necesarios.";				
			}
			else
			{
				next;
				mes .npcname$;			
				mes "Hasta luego!";
			}			
			garbagecol();
		close;
		
		
		case 2:
		close;
	}
end;

//Functions Bodies
	function updatePoints {
		set .@account_id,getarg(0);
		set .@usedPoints,getarg(1);
		query_sql("UPDATE `cp_v4p_voters` SET points=(points-"+.@usedPoints+") WHERE account_id='"+.@account_id+"'");
		return;
	}

	function getPoints {
		set .@account_id,getarg(0);
		query_sql("SELECT `points` FROM `cp_v4p_voters` WHERE account_id="+.@account_id+" LIMIT 1",.@points);
		if(getarraysize(.@points)==0)
			return 0;		
		return .@points[0];
	}

	function getItemDetails {
		set .@key,getarg(0); //Key
		set .@detail$,getarg(1); //What details to return such as ItemID, Points, Quantity, Category
		
		if(strtolower(.@detail$) == strtolower("ItemID"))
			return @itemID[.@key];
		else if(strtolower(.@detail$) == strtolower("Quantity"))
			return @itemQ[.@key];
		else if(strtolower(.@detail$) == strtolower("Points"))
			return @points[.@key];
		else if(strtolower(.@detail$) == strtolower("Category"))
			return @category$[.@key];
	}

	function getItemsByCat {		
		set .@selectedCat$,getarg(0);
		set .@make_string$,"";
		set .@x,0;
		for(set .@i,0; .@i<getarraysize(@category$); set .@i,.@i+1)
		{
			if(strtolower(.@selectedCat$) == strtolower(@category$[.@i]))
			{
				setarray @itemKeys[.@x],.@i;
				if(.@make_string$ == "")
					set .@make_string$,getitemname(@itemID[.@i]);
				else
					set .@make_string$,.@make_string$+":"+getitemname(@itemID[.@i]);
				
				set .@x,.@x+1;
			}
		}
		return .@make_string$;
	}
	
	
	function makeCategory {
		set .@make_string$,"";
		for(set .@i,0; .@i<getarraysize(@category$); set .@i,.@i+1)
		{			
			if(.@make_string$ == "")
			{
				setarray @listCat$[getarraysize(@listCat$)],@category$[.@i];
				set .@make_string$,@category$[.@i];
			}
			else
			{
				if(compare(.@make_string$,@category$[.@i])==0)
				{
					setarray @listCat$[getarraysize(@listCat$)],@category$[.@i];
					set .@make_string$,.@make_string$+":"+@category$[.@i];				
				}
			}
		}
		return .@make_string$;
	}
			
	function add_item
	{
		set .@itemID,getarg(0,-1); //IteID
		set .@itemQ,getarg(1,-1); //Item Quantity
		set .@points,getarg(2,-1);
		set .@cat$,getarg(3,"Uncategorized"); //Category
		
		
		if(.@itemID == -1)
		{
			debugmes "Invalid Item ID. Script not completely loaded.";
			end;
		}
		else if(.@itemQ == -1)
		{
			debugmes "Invalid Item Quantity. Script not completely loaded.";
			end;
		}
		else if(.@points == -1)
		{
			debugmes "Points assignment error. Script not completely loaded.";
			end;
		}
		set .@key,getarraysize(@itemID);
		setarray @itemID[.@key],.@itemID;
		setarray @itemQ[.@key],.@itemQ;
		setarray @points[.@key],.@points;
		setarray @category$[.@key],.@cat$;
		return 1; //return 1 as success
	}
	
	function garbagecol{
		deletearray @itemID[0],128;
		deletearray @itemQ[0],128;
		deletearray @points[0],128;	
		deletearray @category$[0],128;	
		deletearray @listCat$[0],128;
		deletearray @itemKeys[0],128;
		return;
	}
}