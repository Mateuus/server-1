//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Kagerou/Oboro Gear Enchants
//================= Description ===========================================
//= Adds enchantments to Kagerou/Oboro gear.
//================= Current Version =======================================
//= 1.0
//=========================================================================

//== Main NPC :: job_ko ====================================
invek,127,99,0	script	Artesano Tene#ko	4_M_JPN2,{
	disable_items;
	mes "[Artesano Tene]";
	mes "¿Hola? Soy Artesano Tene, el más respetuoso mago de todos los tiempos..";
	next;
	mes "[Artesano Tene]";
	mes "Como te puedo ayudar?";
	next;
	switch (select("¿Mago?", "Agregar Encantamiento.", "Remover Encantamiento.")) {
	case 1: // Which blacksmith?
		mes "[Artesano Tene]";
		mes "Oh! Lo lamento, no me he presentado.";
		next;
		mes "[Artesano Tene]";
		mes "Soy una especie de mago que ha dominado el arte de los encantamientos, puedo quitar y agregar ranuras encantadas de algunas armas y armaduras a un precio pequeño.";
		next;
		mes "[Artesano Tene]";
		mes "Oh! ¿Quieres saber la lista de armas y armaduras en las que trabajo?";
		next;
		switch (select("Echa un vistazo a la lista de armas.", "Echar un vistado a la lista de Armaduras")) {
		case 1: // Take a look at the weapon list
			mes "[Artesano Tene]";
			mes "¿Estás interesado en la lista de armas?";
			next;
			mes "[Artesano Tene]";
			mes "Estas son las Armas, en las cuales trabajo.";
			mes "^ff0000Raksasa Dagger [1]^000000";
			mes "^ff0000Mikatsuki[1]^000000";
			mes "^ff0000Huuma Swirling Petal [2]^000000";
			mes "^ff0000Huuma Fluttering Snow^000000";
			mes "^ff0000Huuma Thunderstorm^000000";
			break;
		case 2: // Take a look at the armor list
			mes "[Artesano Tene]";
			mes "¿Está interesado en la lista de armaduras?";
			next;
			mes "[Artesano Tene]";
			mes "Estas son las Armaduras, en las cuales trabajo.";
			mes "^ff0000Wolf Armguard [1]^000000";
			mes "^ff0000Crescent Armguard [1]^000000";
			mes "^ff0000Ninja Scale Armor [1]^000000";
			mes "^ff0000Tenebris Latitantes [1]^000000";
			mes "^ff0000Special Ninja Suit [1]^000000";
			break;
		}
		next;
		mes "[Artesano Tene]";
		mes "Eso es todo. Todavía estoy en la práctica para aprender y manejar más variedad de equipamiento.";
		next;
		mes "[Artesano Tene]";
		mes "Si lo desea, puedo agregar encantamiento a sus ranuras 4 y 3 a un costo pequeño.";
		close;
	case 2: // I want to add option
		mes "[Artesano Tene]";
		mes "Oh!, con que quieres agregar un encantamiento.";
		mes "Puedo agregar encantamientos a tus ranuras 4 y 3, solo si tienes el equipamiento equipado.";
		next;
		mes "[Artesano Tene]";
		mes "Puesto que utilizo una forma estable y única de añadir encantamientos, no hay riesgos de ^ff0000'Perder Refinamiento'^000000 o ^ff0000'Perder Cartas'^000000 en el objeto equipado.";
		next;
		mes "[Artesano Tene]";
		mes "Los costos de los materiales son altos, es por eso que sólo pediré un  ^5565AApequeño costo^000000 para mis servicios con la excepción de los gastos materiales.";
		next;
		mes "[Artesano Tene]";
		mes "Je Je, creo que he hablado demasiado. Que deseas encantar?";
		next;
		switch (select("Armas...", "Armadura...")) {
		case 1:
			mes "[Artesano Tene]";
			mes "Ya veo, quieres encantar  tu arma. Eso tiene un valor de ^ff0000100,000 Zeny para un solo servicio. ^000000";
			next;
			mes "[Artesano Tene]";
			mes "^FF0000Ah! Puedo añadir un encantamiento adicionall si tienes equipado un arma con una sola mano, y que esté en la mano derecha, porque es más fácil para mí trabajar.^000000";
			next;
			mes "[Artesano Tene]";
			mes "Cual es la arma que deseas encantar?";
			next;
			switch (select("No ahora...", "Raksasa Dagger [1]", "Mikatsuki [1]", "Huuma Swirling Petal [2]", "Huuma Fluttering Snow", "Huuma Thunderstorm")) {
			case 1:
				mes "[Artesano Tene]";
				mes "Ya veo, has cambiado de parecer. Espero verte pronto.";
				close;
			case 2:
				.@equip_id = Raksasa_Dagger;
				break;
			case 3:
				.@equip_id = Mikatsuki;
				break;
			case 4:
				.@equip_id = Huuma_Swirling_Petal;
				break;
			case 5:
				.@equip_id = Huuma_Fluttering_Snow;
				break;
			case 6:
				.@equip_id = Huuma_Thunderstorm;
				break;
			}
			.@part = EQI_HAND_R;
			break;
		case 2:
			mes "[Artesano Tene]";
			mes "Ya veo, quieres encantar  tu armadura. Eso tiene un valor de ^ff0000100,000 Zeny para un solo servicio.^000000";
			next;
			mes "[Artesano Tene]";
			mes "¿Qué Armadura deseas encantar?";
			next;
			switch (select("No ahora...", "Wolf Armguard [1]", "Crescent Armguard [1]", "Ninja Scale Armor [1]", "Tenebris Latitantes [1]", "Special Ninja Suit [1]")) {
			case 1:
				mes "[Artesano Tene]";
				mes "¿Has cambiado de parecer?. Esta bien, espero verte pronto.";
				close;
			case 2:
				.@part = EQI_HAND_L;
				.@equip_id = Wolf_Armguard;
				break;
			case 3:
				.@part = EQI_HAND_L;
				.@equip_id = Crescent_Armguard;
				break;
			case 4:
				.@part = EQI_ARMOR;
				.@equip_id = Ninja_Scale_Armor;
				break;
			case 5:
				.@part = EQI_ARMOR;
				.@equip_id = Tenebris_Latitantes;
				break;
			case 6:
				.@part = EQI_ARMOR;
				.@equip_id = Special_Ninja_Suit_;
				break;
			}
			break;
		}
		break;
	case 3: // I want to remove option
		mes "[Artesano Tene]";
		mes "Quieres remover un encantamiento. ¿No te ha gustado el que tienes?";
		next;
		mes "[Artesano Tene]";
		mes "Lamento que no te haya gustado el encantamiento seleccionado. Espero que vengas nuevamente.";
		next;
		mes "[Artesano Tene]";
		mes "^ff0000El costo de este servicio es 100,000 Zeny.^000000 Cual encantamiento de la siguiente lista deseas remover?";
		next;
		switch(select("No ahora...", "Raksasa Dagger [1]", "Mikatsuki [1]", "Huuma Swirling Petal [2]", "Huuma Fluttering Snow", "Huuma Thunderstorm", "Wolf Armguard [1]", "Crescent Armguard [1]", "Ninja Scale Armor [1]", "Tenebris Latitantes [1]", "Special Ninja Suit [1]")) {
		case 1:
			mes "[Artesano Tene]";
			mes "Acaso mis servicios son caros?. Voy a trabajar más duro en el desarrollo de mis habilidades y reducir los costos.";
			close;
		case 2:
			.@part = EQI_HAND_R;
			.@equip_id = Raksasa_Dagger;
			break;
		case 3:
			.@part = EQI_HAND_R;
			.@equip_id = Mikatsuki;
			break;
		case 4:
			.@part = EQI_HAND_R;
			.@equip_id = Huuma_Swirling_Petal;
			break;
		case 5:
			.@part = EQI_HAND_R;
			.@equip_id = Huuma_Fluttering_Snow;
			break;
		case 6:
			.@part = EQI_HAND_R;
			.@equip_id = Huuma_Thunderstorm;
			break;
		case 7:
			.@part = EQI_HAND_L;
			.@equip_id = Wolf_Armguard;
			break;
		case 8:
			.@part = EQI_HAND_L;
			.@equip_id = Crescent_Armguard;
			break;
		case 9:
			.@part = EQI_ARMOR;
			.@equip_id = Ninja_Scale_Armor;
			break;
		case 10:
			.@part = EQI_ARMOR;
			.@equip_id = Tenebris_Latitantes;
			break;
		case 11:
			.@part = EQI_ARMOR;
			.@equip_id = Special_Ninja_Suit_;
			break;
		}
		mes "[Artesano Tene]";
		mes "Deseas reiniciar ^44B7BC" + getitemname(.@equip_id) + "^000000?";
		next;
		mes "[Artesano Tene]";
		mes "El costo es el mismo, pero puedo proporcionar un servicio para eliminar sólo el encantamiento en la tercera ranura y dejar la 4ta ranura como esta. ¿Estás interesado?";
		next;
		switch (select("Reiniciar solo el 3er encantamiento.", "Reiniciar Todo.")) {
		case 1:
			mes "[Artesano Tene]";
			mes "Muy bien. Solo reiniciaré el 3er encantamiento.";
			next;
			.@only_3rd = true;
			break;
		case 2:
			mes "[Artesano Tene]";
			mes "Muy bien. Todo será reiniciado.";
			next;
			break;
		}
		.@remove = true;
		break;
	}
	if (getequipisequiped(.@part) == 0) {
		mes "[Artesano Tene]";
		mes "Debes equiparte ^44B7BC" + getitemname(.@equip_id) + "^000000. antes de continuar.";
		close;
	}
	if (Zeny < 100000) { // Custom Translation
		mes "[Artesano Tene]";
		mes "Lo lamento, no tienes el Zeny suficiente.!";
		next;
		mes "[Artesano Tene]";
		mes "Nuestros precios son muy bajos, ten en consideración que debo vivir de algo.";
		close;
	}
	if (getequipid(.@part) != .@equip_id) {
		mes "[Artesano Tene]";
		mes "Este no es el equipamiento correcto.";
		next;
		mes "[Artesano Tene]";
		mes "No sé trabajar con equipamientos que no se encuentran en la lista.";
		close;
	}

	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@equip_card, getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);

	.@enchants = callsub(S_IsEnchanted, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]);
	// Initialization
	if (.@remove) {
		if (!.@enchants) {
			mes "[Artesano Tene]";
			mes "Este equipamiento no esta encantado. Favor verifica que sea el correcto."; //custom translation
			close;
		}
		if (.@only_3rd && .@enchants < 2) {
			mes "[Artesano Tene]";
			mes "La tercera ranura no se encuentra encantada. Favor  verificar que sea el correcto."; //custom translation
			close;
		}
		.@equip_card[2] = 0;
		if (!.@only_3rd)
			.@equip_card[3] = 0;
		progressbar "ffff00",2;
		Zeny -= 100000;
		delequip .@part;
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		mes "[Artesano Tene]";
		mes "Iniciación terminada.";
		close;
	}

	// Enchantment
	if (.@equip_refine <= 4)       .@ko_type01 = 1;
	else if (.@equip_refine <= 7)  .@ko_type01 = 2;
	else if (.@equip_refine <= 9)  .@ko_type01 = 3;
	else if (.@equip_refine <= 12) .@ko_type01 = 4;
	else                           .@ko_type01 = 5;

	//custom translations
	.@available_slots = callsub(S_CanEnchant, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]);
	if (!.@available_slots) {
		mes "[Artesano Tene]";
		mes "Este equipamiento no puede recibir otro encantamiento.";
		next;
		mes "[Artesano Tene]";
		mes "Si no estás satisfecho con el encantamiento, te recomiendo remover ranura encantada.";
		close;
	}
	mes "[Artesano Tene]";
	if (.@available_slots < 2)
		mes "La cuarta ranura ya ha sido encantada. ¿Deseas encantar la tercera ranura también?";
	else
		mes "Este equipo no está encantado actualmente. ¿Deseas encantar la cuarta ranura también?";
	next;
	if (select("No Realizar", "Realizar") == 1) {
		mes "[Artesano Tene]";
		mes "Muy bien. Espero verte pronto";
		close;
	}
	mes "[Artesano Tene]";
	if (.@available_slots < 2)
		mes "Comenzando a encantar la 3ra ranura.";
	else
		mes "Comenzando a encantar la 4ta ranura";
	next;
	if (.@available_slots >= 2) { // slot 4
		.@r = rand(1,1000);
		switch (.@ko_type01) {
		case 1:
			if (.@r <= 200)      .@e_socket04 = Mdef2;
			else if (.@r <= 350) .@e_socket04 = Mdef4;
			else if (.@r <= 450) .@e_socket04 = Mdef6;
			else if (.@r <= 500) .@e_socket04 = Mdef8;
			else if (.@r <= 700) .@e_socket04 = Def3;
			else if (.@r <= 850) .@e_socket04 = Def6;
			else if (.@r <= 950) .@e_socket04 = Def9;
			else                 .@e_socket04 = Def12;
			.@enchant = .@e_socket04;
			break;
		case 2:
			if (.@r <= 90)       .@d_socket04 = Mdef2;
			else if (.@r <= 170) .@d_socket04 = Mdef4;
			else if (.@r <= 240) .@d_socket04 = Mdef6;
			else if (.@r <= 300) .@d_socket04 = Mdef8;
			else if (.@r <= 390) .@d_socket04 = Def3;
			else if (.@r <= 470) .@d_socket04 = Def6;
			else if (.@r <= 540) .@d_socket04 = Def9;
			else if (.@r <= 600) .@d_socket04 = Def12;
			else if (.@r <= 660) .@d_socket04 = Strength1;
			else if (.@r <= 720) .@d_socket04 = Inteligence1;
			else if (.@r <= 780) .@d_socket04 = Vitality1;
			else if (.@r <= 840) .@d_socket04 = Dexterity1;
			else if (.@r <= 900) .@d_socket04 = Agility1;
			else                 .@d_socket04 = Luck1;
			.@enchant = .@d_socket04;
			break;
		case 3:
			if (.@r <= 90)       .@c_socket04 = Strength1;
			else if (.@r <= 180) .@c_socket04 = Inteligence1;
			else if (.@r <= 270) .@c_socket04 = Vitality1;
			else if (.@r <= 360) .@c_socket04 = Dexterity1;
			else if (.@r <= 450) .@c_socket04 = Agility1;
			else if (.@r <= 600) .@c_socket04 = Luck1;
			else if (.@r <= 660) .@c_socket04 = Strength2;
			else if (.@r <= 720) .@c_socket04 = Inteligence2;
			else if (.@r <= 780) .@c_socket04 = Vitality2;
			else if (.@r <= 840) .@c_socket04 = Dexterity2;
			else if (.@r <= 900) .@c_socket04 = Agility2;
			else                 .@c_socket04 = Luck2;
			.@enchant = .@c_socket04;
			break;
		case 4:
			if (.@r <= 70)       .@b_socket04 = Strength1;
			else if (.@r <= 140) .@b_socket04 = Inteligence1;
			else if (.@r <= 210) .@b_socket04 = Vitality1;
			else if (.@r <= 280) .@b_socket04 = Dexterity1;
			else if (.@r <= 350) .@b_socket04 = Agility1;
			else if (.@r <= 440) .@b_socket04 = Luck1;
			else if (.@r <= 495) .@b_socket04 = Strength2;
			else if (.@r <= 550) .@b_socket04 = Inteligence2;
			else if (.@r <= 605) .@b_socket04 = Vitality2;
			else if (.@r <= 660) .@b_socket04 = Dexterity2;
			else if (.@r <= 715) .@b_socket04 = Agility2;
			else if (.@r <= 800) .@b_socket04 = Luck2;
			else if (.@r <= 860) .@b_socket04 = HP100;
			else if (.@r <= 910) .@b_socket04 = HP200;
			else if (.@r <= 950) .@b_socket04 = HP300;
			else if (.@r <= 980) .@b_socket04 = SP50;
			else                 .@b_socket04 = SP100;
			.@enchant = .@b_socket04;
			break;
		case 5:
			if (.@r <= 59)       .@a_socket04 = Strength2;
			else if (.@r <= 118) .@a_socket04 = Inteligence2;
			else if (.@r <= 177) .@a_socket04 = Vitality2;
			else if (.@r <= 236) .@a_socket04 = Dexterity2;
			else if (.@r <= 295) .@a_socket04 = Agility2;
			else if (.@r <= 354) .@a_socket04 = Luck2;
			else if (.@r <= 413) .@a_socket04 = HP100;
			else if (.@r <= 472) .@a_socket04 = HP200;
			else if (.@r <= 530) .@a_socket04 = HP300;
			else if (.@r <= 589) .@a_socket04 = SP50;
			else if (.@r <= 648) .@a_socket04 = SP100;
			else if (.@r <= 707) .@a_socket04 = Strength3;
			else if (.@r <= 765) .@a_socket04 = Inteligence3;
			else if (.@r <= 824) .@a_socket04 = Vitality3;
			else if (.@r <= 883) .@a_socket04 = Dexterity3;
			else if (.@r <= 942) .@a_socket04 = Agility3;
			else                 .@a_socket04 = Luck3;
			.@enchant = .@a_socket04;
			break;
		}
		.@equip_card[2] = .@enchant;
	} else if (.@available_slots == 1) { // slot 3
		.@r = rand(1,1000);
		switch (.@ko_type01) {
		case 1:
			if (.@r <= 125)      .@e_socket03 = Mdef2;
			else if (.@r <= 250) .@e_socket03 = Mdef4;
			else if (.@r <= 375) .@e_socket03 = Mdef6;
			else if (.@r <= 500) .@e_socket03 = Mdef8;
			else if (.@r <= 625) .@e_socket03 = Def3;
			else if (.@r <= 750) .@e_socket03 = Def6;
			else if (.@r <= 875) .@e_socket03 = Def9;
			else                 .@e_socket03 = Def12;
			.@enchant = .@e_socket03;
			break;
		case 2:
			if (.@r <= 100)      .@d_socket03 = Mdef2;
			else if (.@r <= 190) .@d_socket03 = Mdef4;
			else if (.@r <= 270) .@d_socket03 = Mdef6;
			else if (.@r <= 340) .@d_socket03 = Mdef8;
			else if (.@r <= 440) .@d_socket03 = Def3;
			else if (.@r <= 530) .@d_socket03 = Def6;
			else if (.@r <= 610) .@d_socket03 = Def9;
			else if (.@r <= 680) .@d_socket03 = Def12;
			else if (.@r <= 720) .@d_socket03 = Strength1;
			else if (.@r <= 760) .@d_socket03 = Inteligence1;
			else if (.@r <= 800) .@d_socket03 = Vitality1;
			else if (.@r <= 850) .@d_socket03 = Dexterity1;
			else if (.@r <= 880) .@d_socket03 = Agility1;
			else                 .@d_socket03 = Luck1;
			.@enchant = .@d_socket03;
			break;
		case 3:
			if (.@r <= 100)      .@c_socket03 = Strength1;
			else if (.@r <= 200) .@c_socket03 = Inteligence1;
			else if (.@r <= 305) .@c_socket03 = Vitality1;
			else if (.@r <= 410) .@c_socket03 = Dexterity1;
			else if (.@r <= 500) .@c_socket03 = Agility1;
			else if (.@r <= 700) .@c_socket03 = Luck1;
			else if (.@r <= 750) .@c_socket03 = Strength2;
			else if (.@r <= 780) .@c_socket03 = Inteligence2;
			else if (.@r <= 830) .@c_socket03 = Vitality2;
			else if (.@r <= 880) .@c_socket03 = Dexterity2;
			else if (.@r <= 900) .@c_socket03 = Agility2;
			else                 .@c_socket03 = Luck2;
			.@enchant = .@c_socket03;
			break;
		case 4:
			if (.@r <= 70)       .@b_socket03 = Strength1;
			else if (.@r <= 160) .@b_socket03 = Inteligence1;
			else if (.@r <= 250) .@b_socket03 = Vitality1;
			else if (.@r <= 340) .@b_socket03 = Dexterity1;
			else if (.@r <= 400) .@b_socket03 = Agility1;
			else if (.@r <= 500) .@b_socket03 = Luck1;
			else if (.@r <= 540) .@b_socket03 = Strength2;
			else if (.@r <= 580) .@b_socket03 = Inteligence2;
			else if (.@r <= 625) .@b_socket03 = Vitality2;
			else if (.@r <= 670) .@b_socket03 = Dexterity2;
			else if (.@r <= 700) .@b_socket03 = Agility2;
			else if (.@r <= 800) .@b_socket03 = Luck2;
			else if (.@r <= 860) .@b_socket03 = HP100;
			else if (.@r <= 900) .@b_socket03 = HP200;
			else if (.@r <= 920) .@b_socket03 = HP300;
			else if (.@r <= 965) .@b_socket03 = SP50;
			else                 .@b_socket03 = SP100;
			.@enchant = .@b_socket03;
			break;
		case 5:
			if (.@r <= 59)       .@a_socket03 = Strength2;
			else if (.@r <= 118) .@a_socket03 = Inteligence2;
			else if (.@r <= 177) .@a_socket03 = Vitality2;
			else if (.@r <= 236) .@a_socket03 = Dexterity2;
			else if (.@r <= 295) .@a_socket03 = Agility2;
			else if (.@r <= 354) .@a_socket03 = Luck2;
			else if (.@r <= 413) .@a_socket03 = HP100;
			else if (.@r <= 472) .@a_socket03 = HP200;
			else if (.@r <= 530) .@a_socket03 = HP300;
			else if (.@r <= 589) .@a_socket03 = SP50;
			else if (.@r <= 648) .@a_socket03 = SP100;
			else if (.@r <= 707) .@a_socket03 = Strength3;
			else if (.@r <= 765) .@a_socket03 = Inteligence3;
			else if (.@r <= 824) .@a_socket03 = Vitality3;
			else if (.@r <= 883) .@a_socket03 = Dexterity3;
			else if (.@r <= 942) .@a_socket03 = Agility3;
			else                 .@a_socket03 = Luck3;
			.@enchant = .@a_socket03;
			break;
		}
		.@equip_card[2] = .@equip_card[3];
		.@equip_card[3] = .@enchant;
	} else {
		mes "[Artesano Tene]";
		mes "Error inesperado. Favor tomar contacto con el Staff *^_^*";
		close;
	}
	progressbar "0xFFFF00",2;
	Zeny -= 100000;
	delequip .@part;

	getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

	mes "[Artesano Tene]";
	mes "^44B7BC" + getitemname(.@equip_id) + "^000000";
	mes "Encantamiento finalizado."; //custom translation
	close;

/**
 * Checks if the item having the given card set is enchanted
 *
 * callsub(S_IsEnchanted, <card1>, <card2>, <card3>, <card4>)
 *
 * @param card1, card2, card3, card4: card IDs in the respective slots
 * @return the amount of enchants ({0, 1, 2})
 */
S_IsEnchanted:
	.@card1 = getarg(0);
	.@card2 = getarg(1);
	.@card3 = getarg(2);
	.@card4 = getarg(3);
	if (.@card1 == -256 || .@card1 == 254 || .@card1 == 255)
		return 0;
	if (.@card3 >= 4700 && .@card3 < 5000) {
		if (.@card4 >= 4700 && .@card4 < 5000) {
			return 2;
		}
		return 1;
	}
	return 0;

/**
 * Checks if the item can be enchanted
 *
 * callsub(S_CanEnchant, <card1>, <card2>, <card3>, <card4>)
 *
 * @param card1, card2, card3, card4: card IDs in the respective slots
 * @return the amount of available enchant slots ({2, 1, 0})
 */
S_CanEnchant:
	.@card1 = getarg(0);
	.@card2 = getarg(1);
	.@card3 = getarg(2);
	.@card4 = getarg(3);
	if (.@card1 != 0)
		return 0;
	if (.@card2 != 0)
		return 0;
	if (.@card3 != 0)
		return 0;
	if (.@card4 != 0) {
		if (.@card4 < 4700 || .@card4 >= 5000)
			return 0;
		return 1;
	}
	return 2;
}
