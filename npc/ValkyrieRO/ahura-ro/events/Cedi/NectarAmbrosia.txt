

louyang,209,116,4	script	Azthark	4_M_DOCTOR,{
if( countitem( .bca_exchange[0] ) ){
	if( select( "Intercambiar "+getitemname( .bca_exchange[0] )+" en "+getitemname( .bca_exchange[1] ),"Elegir un Tier" ) == 1 ){
		delitem .bca_exchange[0],1;
		getitem .bca_exchange[1],1;		
		mes "Has ganado 1x "+getitemname( .bca_exchange[1] );
		next;
	}
}
mes "Tienes actualmente "+#cedi_point+" puntos.";
mes "";
mes "Selecciona un Nectar de la lista.";
next;
set .@i,select( .tier_menu$ ) - 1;
mes "["+getitemname( .nectar[.@i] ) +" ("+.tier$[.@i]+")]";
mes "Requerimientos :";
if( .bio_point[.@i] )
	mes .bio_point[.@i]+" x Puntos";
if( .bio_sample[.@i] && .bio_amount[.@i] ){
	mes .bio_amount[.@i]+" x "+getitemname( .bio_sample[.@i] );
	if( countitem( .bio_sample[.@i] ) < .bio_amount[.@i] ) close;
}
if( .bio_point[.@i] && #cedi_point < .bio_point[.@i] ) close;
if( select( "Intercambiar en "+getitemname( .nectar[.@i] )+" ("+.tier$[.@i]+")","Cancelar" ) == 1 ){
	if( .bio_point[.@i] ) 
		#cedi_point -= .bio_point[.@i];
	if( .bio_sample[.@i] && .bio_amount[.@i] )
		delitem .bio_sample[.@i],.bio_amount[.@i];
	getitem .nectar[.@i],1;
	mes "Has conseguido un "+getitemname( .nectar[.@i] );
}
close;

OnInit:
// Consummate to Bloody Card Album
setarray .bca_exchange,
	32710,	// item id for Consummate Bio Sample
	5603;	// item id for Bloody Card Album
	
// tiers name
setarray .tier$,"Tier D","Tier C","Tier B","Tier A";
// nectar item id
setarray .nectar,
	32700,	// item id for clear nectar
	32701,	// item id for yellow nectar
	32702,	// item id for red nectar
	32703;	// item id for blue nectar

// Requirements for each tier ( if 0 = none )
setarray .bio_sample,
	0,		// no need bio sample
	32707,	// item id for bio sample
	32708,	// item id for quality bio sample
	32709,	// item id for pristine bio sample
	32710;	// item id for consummate bio sample ( reward for tier A )
	
// required bio sample amount 
setarray .bio_amount,0,2,2,2;
// required cedi point
setarray .bio_point,125,250,500,1000;	

.tier_size = getarraysize( .tier$ );
for( .@i = 0; .@i < .tier_size; .@i++ )
	.tier_menu$ = .tier_menu$ + getitemname( .nectar[.@i] ) + "( "+.tier$[.@i]+" )" +":";
end;
}
















moscovia,222,216,4	script	Rhon	JP_RUFAKU,{
mes "Tienes actualmente "+#cedi_point+" puntos.";
mes "Selecciona un Nectar de la lista.";
next;
set .@i,select( .tier_menu$ ) - 1;
mes "["+getitemname( .nectar[.@i] ) +" ("+.tier$[.@i]+")]";
mes "^777777( Beneficio "+getitemname( .ambrosia[.@i] )+" )^000000";
mes "Requerimientos :";
if( .bio_point[.@i] )
	mes .bio_point[.@i]+" x Puntos";
if( .bio_sample[.@i] && .bio_amount[.@i] ){
	mes .bio_amount[.@i]+" x "+getitemname( .bio_sample[.@i] );
	if( countitem( .bio_sample[.@i] ) < .bio_amount[.@i] ) close;
}
if( .bio_point[.@i] && #cedi_point < .bio_point[.@i] ) close;
if( select( "Intercambiar en "+getitemname( .nectar[.@i] )+" ("+.tier$[.@i]+")","Cancelar" ) == 1 ){
	if( .bio_point[.@i] ) 
		#cedi_point -= .bio_point[.@i];
	if( .bio_sample[.@i] && .bio_amount[.@i] )
		delitem .bio_sample[.@i],.bio_amount[.@i];
	getitem .nectar[.@i],1;
	getitem .ambrosia[.@i],1;
	mes "Has recibido 1 "+getitemname( .nectar[.@i] )+" y 1 "+getitemname( .ambrosia[.@i] );
}
close;

OnInit:
// tiers name
setarray .tier$,"Tier C","Tier B","Tier A";

// ambrosia item id
setarray .ambrosia,
	32704,	// item id for clear ambrosia
	32705,	// item id for yellow ambrosia
	32706;	// item id for red ambrosia

// nectar item id
setarray .nectar,
	32701,	// item id for yellow nectar
	32702,	// item id for red nectar
	32703;	// item id for blue nectar

// Requirements for each tier ( if 0 = none )
setarray .bio_sample,
	32707,	// item id for bio sample
	32708,	// item id for quality bio sample
	32709,	// item id for pristine bio sample
	32710;	// item id for consummate bio sample ( reward for tier A )
	
// required bio sample amount 
setarray .bio_amount,2,2,2;
// required cedi point
setarray .bio_point,325,750,1500;

.tier_size = getarraysize( .tier$ );
for( .@i = 0; .@i < .tier_size; .@i++ )
	.tier_menu$ = .tier_menu$ + getitemname( .nectar[.@i] ) + "( "+.tier$[.@i]+" )" +":";
end;
}
